buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
}

plugins {
	id 'scala'
	id 'java'
	id 'maven-publish'
	id 'com.adtran.scala-multiversion-plugin' version '1.0.34'
}

repositories {
	mavenCentral()
	jcenter()
}

ext {
	sparkVersion = "2.3.0"
}

dependencies {
	compile files("$sourceSets.main.scala.outputDir")
	compile "org.scala-lang:scala-library:%scala-version%"
	compile "com.couchbase.client:java-client:2.7.4"
	compile ("com.couchbase.client:encryption:1.0.0") {
		exclude group: 'com.fasterxml.jackson.core'
	}
	compile "com.couchbase.client:dcp-client:0.22.0"
	compile "io.reactivex:rxscala_%%:0.26.5"
	compile "org.apache.logging.log4j:log4j-api:2.2"

	compileOnly "org.apache.spark:spark-core_%%:$sparkVersion"
	compileOnly "org.apache.spark:spark-streaming_%%:$sparkVersion"
	compileOnly "org.apache.spark:spark-sql_%%:$sparkVersion"

	testCompile "junit:junit:4.12"
	testCompile "org.scalatest:scalatest_%%:2.2.5"
	testCompile "org.apache.spark:spark-core_%%:$sparkVersion"
	testCompile "org.apache.spark:spark-streaming_%%:$sparkVersion"
	testCompile "org.apache.spark:spark-sql_%%:$sparkVersion"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.couchbase.client'
			artifactId = 'spark-connector'
			version = '2.3.0-SNAPSHOT'
			pom {
				description = 'Official Couchbase Spark Connector'
			}

			from components.java
		}
	}
}

tasks.compileJava.dependsOn compileScala
tasks.compileScala.dependsOn.remove("compileJava")

sourceSets {
	main {
		scala {
			outputDir = file("$buildDir/classes/scala/main")
		}
		java {
			outputDir = file("$buildDir/classes/java/main")
		}
	}
}