buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		// Do not upgrade, 4.9.6+ is buggy.
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.15.2"
	}
}

plugins {
	id 'scala'
	id 'java'
	id 'maven-publish'
	id 'ivy-publish'
	id 'com.adtran.scala-multiversion-plugin' version '1.0.34'
	id 'com.github.johnrengelman.shadow' version '6.0.0'
	id 'com.jfrog.artifactory' version '4.15.2'
}

repositories {
	mavenCentral()
	jcenter()
}

ext {
	sparkVersion = "3.0.0"
}

group = 'com.couchbase.client'
version = '3.0.1'

dependencies {
	compile files("$sourceSets.main.scala.outputDir")
	compile "org.scala-lang:scala-library:%scala-version%"
	compile "com.couchbase.client:java-client:2.7.15"
	compile "com.couchbase.client:dcp-client:0.28.0"
	compile "io.reactivex:rxscala_%%:0.27.0"
	compile "org.apache.logging.log4j:log4j-api:2.2"

	compileOnly "org.apache.spark:spark-core_%%:$sparkVersion"
	compileOnly "org.apache.spark:spark-streaming_%%:$sparkVersion"
	compileOnly "org.apache.spark:spark-sql_%%:$sparkVersion"

	testCompile "junit:junit:4.12"
	testCompile "org.scalatest:scalatest_%%:3.0.5"
	testCompile "org.apache.spark:spark-core_%%:$sparkVersion"
	testCompile "org.apache.spark:spark-streaming_%%:$sparkVersion"
	testCompile "org.apache.spark:spark-sql_%%:$sparkVersion"

	// For BulkInsertingSample
	testCompile "com.nrinaudo:kantan.csv_%%:0.5.0"
}

shadowJar {
	classifier = ''

	dependencies {
		include dependency("com.couchbase.client:java-client:2.7.15")
		include dependency("com.couchbase.client:core-io:1.7.15")
	}

	relocate 'com.couchbase.client.core', 'com.couchbase.spark.deps.com.couchbase.client.core'
	relocate 'com.couchbase.client.java', 'com.couchbase.spark.deps.com.couchbase.client.java'
	relocate 'com.couchbase.client.encryption', 'com.couchbase.spark.deps.com.couchbase.client.encryption'
}

sourceSets {
	main {
		scala {
			outputDir = file("$buildDir/classes/scala/main")
		}
		java {
			outputDir = file("$buildDir/classes/java/main")
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
	classifier = 'javadoc'
	from scaladoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives scaladocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'spark-connector'

			artifact tasks.sourcesJar
			artifact tasks.scaladocJar

			pom {
				groupId = 'com.couchbase.client'
				artifactId = 'spark-connector'
				version = '3.0.1'

				name = 'couchbase-spark-connector'
				description = 'Official Couchbase Spark Connector'
				url = 'http://couchbase.com'
				organization {
					name = 'Couchbase, Inc.'
					url = 'http://couchbase.com'
				}
				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution = 'repo'
					}
				}
				scm {
					url = 'https://github.com/couchbase/couchbase-spark-connector'
					connection = 'scm:git:git://github.com/couchbase/couchbase-spark-connector'
					developerConnection = 'scm:git:git://github.com/couchbase/couchbase-spark-connector'
				}
				issueManagement {
					system = "Couchbase JIRA"
					url = "http://www.couchbase.com/issues/browse/SPARKC"
				}
				developers {
					developer {
						id = 'daschl'
						name = 'Michael Nitschinger'
						email = 'michael.nitschinger@couchbase.com'
					}
					developer {
						id = 'programmatix'
						name = 'Graham Pople'
						email = 'graham.pople@couchbase.com'
					}
					developer {
						id = 'dnault'
						name = 'David Nault'
						email = 'david.nault@couchbase.com'
					}
				}
			}

			from components.java
		}
	}
}